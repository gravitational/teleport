---
title: "Part 1: Enroll Infrastructure with Terraform"
h1: "Terraform Starter: Enroll Infrastructure"
description: Explains you how to deploy a pool of Teleport Agents so you can apply dynamic resources to enroll your infrastructure with Teleport.
---

*This guide is Part One of the Teleport Terraform starter guide. Read the
[overview](../terraform-starter.mdx) for the scope and purpose of the Terraform
starter guide.*

This guide shows you how to use Terraform to enroll infrastructure resources
with Teleport. You will:

- Deploy a pool of Teleport Agents running on virtual machines.
- Label resources enrolled by the Agents with `env:dev` and `env:prod` so that,
  in [Part Two](rbac.mdx), you can configure Teleport roles to enable access to
  these resources.

## How it works

An Agent is a Teleport instance configured to run one or more Teleport services
in order to proxy infrastructure resources. For a brief architectural overview
of how Agents run in a Teleport cluster, read the [Introduction to Teleport
Agents](introduction.mdx).

There are several methods you can use to join a Teleport Agent to your cluster,
which we discuss in the [Joining Services to your
Cluster](join-services-to-your-cluster.mdx) guide. In this guide, we will use
the **join token** method, where the operator stores a secure token on the Auth
Service, and an Agent presents the token in order to join a cluster.

No matter which join method you use, it will involve the following Terraform
resources:

- Compute instances to run Teleport services
- A join token for each compute instance in the Agent pool

## Prerequisites

(!docs/pages/includes/edition-prereqs-tabs.mdx version="16.2.0"!)

<Admonition type="tip">

We recommend following this guide on a fresh Teleport demo cluster so you can
see how an Agent pool works. After you are familiar with the setup, apply the
lessons from this guide to protect your infrastructure. You can get started with
a demo cluster using:
- A demo deployment on a [Linux server](../../../deploy-a-cluster/linux-demo.mdx)
- A [Teleport Enterprise (Cloud) trial](https://goteleport.com/signup)

</Admonition>

- An AWS, Google Cloud, or Azure account with permissions to create virtual
  machine instances.
  
- Cloud infrastructure that enables virtual machine instances to connect to the
  Teleport Proxy Service. For example:
  - An AWS subnet with a public NAT gateway or NAT instance
  - Google Cloud NAT
  - Azure NAT Gateway

  In minimum-security demo clusters, you can also configure the VM instances you
  deploy to have public IP addresses.

- Terraform v(=terraform.version=) or higher.

- (!docs/pages/includes/tctl.mdx!)

## Step 1/4. Import the Terraform module

In this step, you will download Terraform modules that show you how to get
started enrolling Teleport resources. These modules are minimal examples of how
Teleport Terraform resources work together to enable you to manage Teleport
Agents. 

After finishing this guide and becoming familiar with the setup, you should
modify your Terraform configuration to accommodate your infrastructure in
production.

1. Navigate to your Terraform project directory.

   <Notice type="warning">
   
   If you don't have an identity file available in
   `terraform-identity/identity`, make sure you have followed the [prerequisites
   for this guide](#prerequisites).
   
   </Notice>

1. Fetch the Teleport code repository and copy the example Terraform
   configuration for this project into your current working directory. The
   following commands copy the appropriate child module for your cloud provider
   into a subdirectory called `cloud` and HCL configurations for Teleport
   resources into a subdirectory called `teleport`:

   <Tabs>
   <TabItem label="AWS">

   ```code
   $ git clone --depth=1 https://github.com/gravitational/teleport teleport-clone
   $ cp -R teleport-clone/examples/terraform-starter/agent-installation teleport
   $ cp -R teleport-clone/examples/terraform-starter/aws cloud
   $ rm -rf teleport-clone
   ```

   </TabItem>
   <TabItem label="Google Cloud">

   ```code
   $ git clone --depth=1 https://github.com/gravitational/teleport teleport-clone
   $ cp -R teleport-clone/examples/terraform-starter/agent-installation teleport
   $ cp -R teleport-clone/examples/terraform-starter/gcp cloud
   $ rm -rf teleport-clone
   ```

   </TabItem>
   <TabItem label="Azure">

   ```code
   $ git clone --depth=1 https://github.com/gravitational/teleport teleport-clone
   $ cp -R teleport-clone/examples/terraform-starter/agent-installation teleport
   $ cp -R teleport-clone/examples/terraform-starter/azure cloud
   $ rm -rf teleport-clone
   ```

   </TabItem>
   </Tabs>

1. Create a file called `agent.tf` with the following content, which imports the
   `terraform-starter` module for your cloud provider:

   <Tabs>
   <TabItem label="AWS">
   
   ```hcl
    module "agent_installation" {
     source                = "./teleport"
     agent_count           = 2
     agent_roles           = ["Node"]
     labels                = {}
     proxy_service_address = "teleport.example.com:443"
     teleport_edition      = "cloud"
     teleport_version      = "(=teleport.version=)"
   }
   
    module "agent_deployment" {
     region           = ""
     source           = "./cloud"
     subnet_id        = ""
     userdata_scripts = module.agent_installation.userdata_scripts
   }
   ```
   
   </TabItem>
   <TabItem label="Google Cloud">
   
   ```hcl
    module "agent_installation" {
     source                = "./teleport"
     agent_count           = 2
     agent_roles           = ["Node"]
     labels                = {}
     proxy_service_address = "teleport.example.com:443"
     teleport_edition      = "cloud"
     teleport_version      = "(=teleport.version=)"
   }
   
    module "agent_deployment" {
     gcp_zone         = "us-east1-b"
     google_project   = ""
     source           = "./cloud"
     subnet_id        = ""
     userdata_scripts = module.agent_installation.userdata_scripts
   }
   ```
   
   </TabItem>
   <TabItem label="Azure">
   
   ```hcl
    module "agent_installation" {
     source                = "./teleport"
     agent_count           = 2
     agent_roles           = ["Node"]
     labels                = {}
     proxy_service_address = "teleport.example.com:443"
     teleport_edition      = "cloud"
     teleport_version      = "(=teleport.version=)"
   }
   
    module "agent_deployment" {
     azure_resource_group = ""
     public_key_path      = ""
     region               = "East US"
     source               = "./cloud"
     subnet_id            = ""
     userdata_scripts     = module.agent_installation.userdata_scripts
   }
   ```
   
   </TabItem>
   </Tabs>

Edit the ` module "agent_installation"` block in `agent.tf` as follows:

1. Assign `agent_count` to `2` for high availability. As you scale your Teleport
   usage, you can increase this count to ease the load on each Agent. 

1. Modify the elements of the `agent_roles` list.

   The Teleport Auth Service associates a join token with one or more roles,
   identifying the Teleport services that are allowed to use the token. Modify
   `agent_roles` to include the token roles that correspond to the Teleport
   services you plan to run on your Agent nodes:

   | Role value  | Service it enables           |
   |-------------|------------------------------|
   | `App`       | Teleport Application Service |
   | `Discovery` | Teleport Discovery Service   |
   | `Db`        | Teleport Database Service    |
   | `Kube`      | Teleport Kubernetes Service  |
   | `Node`      | Teleport SSH Service         |

   The elements of the `agent_roles` list also determine which Teleport services
   the Agents deployed by the `terraform-starter` module will run. For
   example, if the value of `agent_roles` is `["Node", "Db"]`, the configuration
   file enables the Teleport SSH Service and Teleport Database Service.
   
   If the value of the `agent_roles` input variable includes the `Node` role,
   the configuration also adds the `role: agent-pool` label to the Teleport SSH
   Service on each instance. This makes it easier to access hosts in the Agent
   pool later. The script makes Teleport the only option for accessing Agent
   instances by disabling OpenSSH on startup and deleting any authorized public
   keys.

1. Assign `proxy_service_address` to the host and HTTPS port of your Teleport
   Proxy Service, e.g., `mytenant.teleport.sh:443`.

   <Notice type="tip">
   
   Make sure to include the port.
   
   </Notice>

1. Make sure `teleport_edition` matches your Teleport edition. Assign this to
   `oss`, `cloud`, or `enterprise`. The default is `oss`.

1. If needed, change the value of `teleport_version` to the version of Teleport
   you want to run on your Agents. It must be either the same major version as
   your Teleport cluster or one major version behind.

Edit the `module "agent_deployment"` block in `agent.tf` as follows:

1. If you are deploying your instance in a minimum-security demo environment and
   do not have a NAT gateway, NAT instance, or other method for connecting your
   instances to the Teleport Proxy Service, modify the `module` block to
   associate a public IP address with each Agent instance: 

   ```hcl
   insecure_direct_access=true
   ```

1. Assign the remaining input variables depending on your cloud provider:

<Tabs>
<TabItem label="AWS">

1. Assign `region` to the AWS region where you plan to deploy Teleport Agents,
   such as `us-east-1`.
1. For `subnet_id`, include the ID of the subnet where you will deploy Teleport
   Agents.

</TabItem>
<TabItem label="Google Cloud">

1. Assign `google_project` to the name of your Google Cloud project and
   `gcp_zone` to the zone where you will deploy Agents, such as `us-east1-b`.
1. For `subnet_id`, include the name or URI of the Google Cloud subnet where you
   will deploy the Teleport Agents.

   The subnet URI has the format:

   ```
   projects/PROJECT_NAME/regions/REGION/subnetworks/SUBNET_NAME
   ```

</TabItem>
<TabItem label="Azure">

1. Assign `azure_resource_group` to the name of the Azure resource group where
   you are deploying Teleport Agents.

1. The module uses `public_key_path` to pass validation, as Azure VMs must
   include an RSA public key with at least 2048 bits. Once the module deploys
   the VMs, a cloud-init script removes the public key and disables OpenSSH. Set
   this input to the path to a valid public SSH key.

1. Assign `region` to the Azure region where you plan to deploy Teleport Agents,
   such as `East US`.

1. For `subnet_id`, include the ID of the subnet where you will deploy Teleport
   Agents. Use the following format:

   ```text
   /subscriptions/SUBSCRIPTION/resourceGroups/RESOURCE_GROUP/providers/Microsoft.Network/virtualNetworks/NETWORK_NAME/subnets/SUBNET_NAME
   ```

</TabItem>
</Tabs>

## Step 2/4. Add provider configurations

In this step, you will configure the `terraform-starter` module for your
Teleport cluster and cloud provider.

In your Terraform project directory, ensure that the file called `provider.tf`
includes the following content:

<Tabs>
<TabItem label="AWS">

```hcl
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.0"
    }

    teleport = {
      source  = "terraform.releases.teleport.dev/gravitational/teleport"
      version = "~> (=teleport.major_version=).0"
    }
  }
}

provider "aws" {
  region = AWS_REGION
}

provider "teleport" {
  # Update addr to point to your Teleport Enterprise (managed) tenant URL's host:port
  addr               = PROXY_SERVICE_ADDRESS
  identity_file_path = "terraform-identity/identity"
}
```

Replace the following placeholders:

| Placeholder           | Value                                                                        |
|-----------------------|------------------------------------------------------------------------------|
| AWS_REGION            | The AWS region where you will deploy Agents, e.g., `us-east-2`               |
| PROXY_SERVICE_ADDRESS | The host and port of the Teleport Proxy Service, e.g., `example.teleport.sh:443` |

</TabItem>
<TabItem label="Google Cloud">
```hcl
terraform {
  required_providers {
    google = {
      source  = "hashicorp/google"
      version = "~> 5.5.0"
    }

    teleport = {
      source  = "terraform.releases.teleport.dev/gravitational/teleport"
      version = "~> (=teleport.major_version=).0"
    }
  }
}

provider "google" {
  project = GOOGLE_CLOUD_PROJECT
  region  = GOOGLE_CLOUD_REGION
}

provider "teleport" {
  # Update addr to point to your Teleport Enterprise (managed) tenant URL's host:port
  addr               = PROXY_SERVICE_ADDRESS
  identity_file_path = "terraform-identity/identity"
}
```

Replace the following placeholders:

| Placeholder           | Value                                                                        |
|-----------------------|------------------------------------------------------------------------------|
| GOOGLE_CLOUD_PROJECT| Google Cloud project where you will deploy Agents. |
| GOOGLE_CLOUD_REGION | Google Cloud region where you will deploy Agents. |
| PROXY_SERVICE_ADDRESS | The host and port of the Teleport Proxy Service, e.g., `example.teleport.sh:443` |


</TabItem>
<TabItem label="Azure">
```hcl
terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 3.0.0"
    }

    teleport = {
      source  = "terraform.releases.teleport.dev/gravitational/teleport"
      version = "~> (=teleport.major_version=).0"
    }
  }
}

provider "teleport" {
  identity_file_path = "terraform-identity/identity"
  # Update addr to point to your Teleport Cloud tenant URL's host:port
  addr               = PROXY_SERVICE_ADDRESS
}

provider "azurerm" {
  features {}
}
```

Replace the following placeholders:

|Placeholder|Value|
|---|---|
| PROXY_SERVICE_ADDRESS | The host and port of the Teleport Proxy Service, e.g., `example.teleport.sh:443` |

</TabItem>
</Tabs>

## Step 3/4. Enroll dynamic infrastructure resources

In this section, you will configure your Agent pool to enroll infrastructure
resources in your Teleport cluster. To do so, you will declare **dynamic
resources**. Agents read these from the Auth Service and proxy traffic to them.
You apply these by defining them as Terraform resources.

The Teleport Terraform provider currently supports the following dynamic
resources:

|Infrastructure Resource|Terraform Resource|
|---|---|
|Application|`teleport_app`|
|Database|`teleport_database`|
|OpenSSH Servers|`teleport_server`|

For Windows desktops and Kubernetes clusters, you must edit the Agent
configuration file to enroll these resources (we will explain this [later in the
guide](#enroll-static-resources).

The module we demonstrate in this guide already configures the Agents you deploy
to be Teleport SSH Service instances. In this setup, you can enroll OpenSSH
servers with your Teleport cluster agentlessly as dynamic resources.

To declare a dynamic resource with Terraform:

1. Add a configuration block similar to the ones below to a `*.tf` file in your
   `terraform-starter` project directory. 

   <Tabs>
   <TabItem label="Application">
   
   ```hcl
   resource "teleport_app" "example" {
     metadata = {
       name        = "example"
       description = "Test app"
       labels = {
         // Teleport adds this label by default, so add it here to
         // ensure a consistent state.
         "teleport.dev/origin" = "dynamic"
         // Change "dev" to "prod" as appropriate.
         "env" = "dev"
       }
     }
   
     spec = {
       uri = "example.com:3000"
     }
   }
   ```

   1. Update the `spec.uri` field to match your application's domain name and
      port.
   
   1. Read the [Terraform Provider
      Reference](../../reference/terraform-provider/resources/app.mdx) for the
      `teleport_app` resource and adjust any additional configuration fields.
   
   </TabItem>
   <TabItem label="Database">
   
   ```hcl
   resource "teleport_database" "example" {
     metadata = {
       name        = "example"
       description = "Test database"
       labels = {
         // Teleport adds this label by default, so add it here to
         // ensure a consistent state.
         "teleport.dev/origin" = "dynamic"
         // Change "dev" to "prod" as appropriate.
         "env" = "dev"
       }
     }
   
     spec = {
       protocol = "postgres"
       uri      = "localhost"
     }
   }
   ```

   1. Update the `protocol` field to match the protocol of your database. Run the
      following command on your workstation to see all supported protocols:

      ```code
      $ teleport help db configure create
      ```

   1. Update the `uri` field to match the domain name port of your database. If
      you manage your database with Terraform, you can set it to an attribute of
      the corresponding Terraform resource to establish a dependency
      relationship.
   1. Read the [Terraform Provider
      Reference](../../reference/terraform-provider/resources/database.mdx) for
      the `teleport_database` resource and adjust any additional configuration
      fields.
   
   </TabItem>
   <TabItem label="Agentless OpenSSH Server">

   ```hcl
   resource "teleport_server" "openssh_agentless" {
     version = "v2"
     sub_kind = "openssh"
     metadata = {
       name        = "example"
       description = "Test server"
       labels = {
           // Teleport adds this label by default, so add it here to
           // ensure a consistent state.
           "teleport.dev/origin" = "dynamic"
           // Change "dev" to "prod" as appropriate.
           "env" = "dev"
         }
       }
     spec = {
       addr = "198.51.100.1:22"
       hostname = "example"
     }
   }
   ```

   1. Update `spec.addr` with the domain name or IP address of the server as well
      as the port. If you manage your database with Terraform, you can set it to
      an attribute of the corresponding Terraform resource to establish a
      dependency relationship.
   1. Update `hostname` with the hostname of the server as shown by Teleport
      client tools (Teleport Connect, `tsh`, and the Web UI). 
   1. Read the [Terraform Provider
      Reference](../../reference/terraform-provider/resources/server.mdx) for
      the `teleport_database` resource and adjust any additional configuration
      fields.

   </TabItem>
   </Tabs>

1. Make sure that, in each dynamic infrastructure resource, the `labels` field
   includes the following line to ensure that your Terraform state remains
   consistent with the your cluster:

   ```hcl
   "teleport.dev/origin" = "dynamic"
   ```

1. Edit the `labels` field of each dynamic infrastructure resource to add
   one of the following key-value pairs:
   - `"env" = "dev"`
   - `"env" = "prod"`

   In the next guide in this series, you will define a Teleport role that
   restricts access to resources with this label.

## Step 4/4. Verify the deployment

In this step, you will create a Teleport bot to apply your Terraform
configuration. The bot will exist for one hour and will be granted the default
`terraform-provider` role that can edit every resource the TF provider supports.

1. Navigate to your Terraform project directory and run the following command.
   The `eval`command assigns environment variables in your shell to credentials
   for the Teleport Terraform provider:

   ```code
   $ eval "$(tctl terraform env)"
   üîë Detecting if MFA is required
   This is an admin-level action and requires MFA to complete
   Tap any security key
   Detected security key tap
   ‚öôÔ∏è Creating temporary bot "tctl-terraform-env-82ab1a2e" and its token
   ü§ñ Using the temporary bot to obtain certificates
   üöÄ Certificates obtained, you can now use Terraform in this terminal for 1h0m0s
   ```

1. Make sure your cloud provider credentials are available to Terraform using
   the standard approach for your organization.

1. Apply the Terraform configuration:

   ```code
   $ terraform init
   $ terraform apply
   ```

1. Once the `apply` command completes, run the following command to verify that
   your Agents have deployed successfully. This command, which assumes that the
   Agents have the `Node` role, lists all Teleport SSH Service instances with
   the `role=agent-pool` label:

   ```code
   $ tsh ls role=agent-pool
   Node Name                  Address    Labels
   -------------------------- ---------- ---------------
   ip-10-1-1-187.ec2.internal ‚üµ Tunnel   role=agent-pool
   ip-10-1-1-24.ec2.internal  ‚üµ Tunnel   role=agent-pool
   ```

## Further reading: How the module works

In this section, we explain the resources configured in the
`terraform-starter` module.

### Join token

The `terraform-starter` module deploys one virtual machine instance for each
Teleport Agent. Each Agent joins the cluster using a token. We create each token
using the `teleport_provision_token` Terraform resource, specifying the token's
value with a `random_string` resource:

```hcl
(!examples/terraform-starter/agent-installation/token.tf!)
```

When we apply the `teleport_provision_token` resources, the Teleport Terraform
provider creates them on the Teleport Auth Service backend. 

### User data script

Each Teleport Agent deployed by the `terraform-starter` module loads a user
data script that creates a Teleport configuration file for the Agent. The
services enabled by the configuration file depend on the value of the
`agent_roles` input variable:

```text
(!examples/terraform-starter/agent-installation/userdata!)
```

### Virtual machine instances

Each cloud-specific child module of `terraform-starter` declares resources to
deploy a virtual machine instance on your cloud provider:

<Tabs>
<TabItem label="AWS">

`ec2-instance.tf` declares a data source for an Amazon Linux 2023 machine image
and uses it to launch EC2 instances that run Teleport Agents with the
`teleport_provision_token` resource:

```hcl
(!examples/terraform-starter/aws/ec2-instance.tf!)
```

</TabItem>
<TabItem label="Google Cloud">

`gcp-instance.tf` declares Google Compute Engine instances that use the
`teleport_provision_token` to run Teleport Agents:

```hcl
(!examples/terraform-starter/gcp/gcp-instance.tf!)
```

</TabItem>
<TabItem label="Azure">

`azure-instance.tf` declares an Azure virtual machine resource to run Teleport
Agents using the `teleport_provision_token` resource, plus the required network
interface for each instance. 

Note that while Azure VM instances require a user account, this module declares
a temporary one to pass validation, but uses Teleport to enable access to the
instances:

```hcl
(!examples/terraform-starter/azure/azure-instance.tf!)
```

</TabItem>
</Tabs>

## Next steps: More options for enrolling resources

In Part One of the Terraform starter guide, we showed you how to use Terraform
to deploy a pool of Teleport Agents in order to enroll infrastructure resources
with Teleport. While the guide showed you how to enroll resources dynamically,
by declaring Terraform resources for each infrastructure resource you want to
enroll, you can protect more of your infrastructure with Teleport by:

- Configuring Auto-Discovery
- Configuring static resource enrollment

### Configure Auto-Discovery

For a more scalable approach to enrolling resources than the one shown in this
guide, configure the Teleport Discovery Service to automatically detect
resources in your infrastructure and enroll them with the Teleport Auth Service.

To configure the Teleport Discovery Service, edit the userdata script run by the
Agent instances managed in the Terraform starter module. Follow the
[Auto-Discovery
guides](../../../enroll-resources/auto-discovery/auto-discovery.mdx) guides to
configure the Discovery Service and enable your Agents to proxy the resources
that the service enrolls.

### Enroll static resources

You can enroll resources statically by editing the configuration file that each
Teleport Agent reads when it starts up, which is `/etc/teleport.yaml` by
default. For information about enrolling static resources, read the
documentation section for each kind of resource you would like to enroll:

- [Databases](../../../enroll-resources/database-access/database-access.mdx)
- [Windows
  desktops](../../../enroll-resources/desktop-access/desktop-access.mdx)
- [Kubernetes
  clusters](../../../enroll-resources/kubernetes-access/kubernetes-access.mdx)
- [Linux servers](../../../enroll-resources/server-access/server-access.mdx)
- [Web applications and cloud provider
  APIs](../../../enroll-resources/application-access/application-access.mdx)

To incorporate configuration changes into your Terraform module:

1. Edit the userdata script for the compute instances that run Teleport Agents.

1. In the section of the configuration file for the service that you edited,
   assign the `labels` field to add key-value pairs. In the next guide in this
   series, you will define a Teleport role that restricts access to resources
   with this label.

   For example, you could indicate that a resource belongs to a development
   environment with the following label:

   ```yaml
   kubernetes_service:
      enabled: true
      labels:
        env: "dev"
   ```

